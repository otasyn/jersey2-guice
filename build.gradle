plugins {
  id 'groovy'
  id 'java'
  id 'war'
  id 'org.gretty' version '3.1.1'
}

group = 'org.otasyn.template'
version = '0.1.0-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_20

repositories {
  mavenLocal()
  mavenCentral()
}

ext {
  /* dependencies */
  jaxbVersion = '2.3.1' // Highest versions without using Jakarta EE.
  jerseyVersion = '2.39.1' // Highest versions without using Jakarta EE.
  guiceBridgeVersion = '2.6.1' // Highest versions without using Jakarta EE.
  guiceVersion = '7.0.0'
  servletApiVersion = '4.0.1' // Highest versions without using Jakarta EE.
  shiroVersion = '1.11.0'
  slf4jVersion = '2.0.7'
  typesafeConfigVersion = '1.4.2'
  urlRewriteVersion = '4.0.3'

  /* test dependencies */
  groovyVersion = '4.0.12'
  spockVersion = '2.3-groovy-4.0'

  /* client dependencies */
  webjarVersion = '0.1.0-SNAPSHOT'
}

dependencies {

  /*******************************/
  /* Implementation Dependencies */
  /*******************************/

  /* JAX-B */
  implementation "javax.xml.bind:jaxb-api:${jaxbVersion}"
  implementation "org.glassfish.jaxb:jaxb-runtime:${jaxbVersion}"

  /* Jersey */
  implementation "org.glassfish.jersey.containers:jersey-container-servlet:${jerseyVersion}"
  implementation "org.glassfish.jersey.ext:jersey-mvc-jsp:${jerseyVersion}"
  implementation "org.glassfish.jersey.inject:jersey-hk2:${jerseyVersion}"

  /* Guice */
  implementation "com.google.inject:guice:${guiceVersion}"

  /* Guice Bridge */
  implementation "org.glassfish.hk2:guice-bridge:${guiceBridgeVersion}"

  /* Shiro */
  implementation "org.apache.shiro:shiro-core:${shiroVersion}"
  implementation "org.apache.shiro:shiro-web:${shiroVersion}"
  implementation "org.apache.shiro:shiro-jaxrs:${shiroVersion}"

  /* SLF4J */
  implementation "org.slf4j:slf4j-api:${slf4jVersion}"
  implementation "org.slf4j:slf4j-log4j12:${slf4jVersion}"
  implementation "org.slf4j:jcl-over-slf4j:${slf4jVersion}"

  /* Typesafe Config */
  implementation "com.typesafe:config:${typesafeConfigVersion}"

  /* UrlRewriteFilter */
  implementation "org.tuckey:urlrewritefilter:${urlRewriteVersion}"

  /*********************************/
  /* Provided Compile Dependencies */
  /*********************************/

  /* Servlet */
  providedCompile "javax.servlet:javax.servlet-api:${servletApiVersion}"

  /************************************/
  /* Test Implementation Dependencies */
  /************************************/

  /* Groovy */
  testImplementation "org.apache.groovy:groovy-all:${groovyVersion}"

  /* Spock */
  testImplementation platform("org.spockframework:spock-bom:${spockVersion}")
  testImplementation "org.spockframework:spock-core"
  testImplementation "org.spockframework:spock-guice"

  /******************************/
  /* Custom Client Dependencies */
  /******************************/

  /* Angular-Webjar */
  implementation "org.otasyn.template:angular-webjar:${webjarVersion}"
}

configurations.all {
  exclude group:'org.slf4j', module:'log4j-over-slf4j'
}

/**
 * Gretty
 *
 * Tasks
 *   :appRun   - Spin up a servlet container
 *   :appStart - Spin up a servlet container and can be stopped with :appStop
 *   :appStop  - Stop a servlet container spun up with :appStart
 *
 * Configuration
 *   https://gretty-gradle-plugin.github.io/gretty-doc/Gretty-configuration.html
 */
gretty {
  contextPath 'jersey2-guice'

  servletContainer 'tomcat9'
  // servletContainer 'jetty9.4'
}
